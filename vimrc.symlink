" vim-plug and plugin settings {{{

" download vim-plug if it's not installed already
if empty(glob('~/.vim/autoload/plug.vim'))
    silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
                \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

" Start vim-plug and list plugins
call plug#begin('~/.vim/plugged')

Plug '/usr/local/opt/fzf'                   " Fzf is installed locally through brew
Plug 'flazz/vim-colorschemes'               " Collection of colorschemes
Plug 'junegunn/fzf.vim'                     " Vim fzf bindings
Plug 'sgur/vim-editorconfig'                " Editorconfig support
Plug 'tpope/vim-commentary'                 " Un/comment easily (gcc)
Plug 'tpope/vim-surround'                   " Surround objects with (,[,{,' etc.
" YouCompleteMe
Plug 'Valloric/YouCompleteMe', { 'do': './install.py' }
Plug 'mattn/emmet-vim'                      " Emmet plugin. Usage: <C-y>, to expand

" End vim-plug listing
call plug#end()

" Customize fzf colors
let g:fzf_colors =  {
  \ 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'border':  ['fg', 'Ignore'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

" }}}

" Colors {{{

colorscheme dracula         " Color scheme/theme

" Customize highlight colors with autocmd to override colorschemes
augroup Colors
    autocmd!
    " Disable background to be consistent with tmux active panes
    autocmd ColorScheme * highlight NonText ctermbg=NONE
    autocmd ColorScheme * highlight Normal ctermbg=NONE
    " Customize the current statusline to be more discernible in Dracula colorscheme
    autocmd ColorScheme * highlight StatusLine ctermbg=176
augroup END

syntax enable               " Enable syntax processing/highlighting

" }}}

" Indentation {{{

set shiftwidth=4            " Actual number of spaces for each indentation step
set tabstop=4               " Number of visual spaces per TAB
set softtabstop=4           " Number of spaces in tab when editing
set expandtab               " Tabs are spaces

set autoindent
set smartindent

" }}}

" UI Layout {{{

set number                  " Show line numbers
set relativenumber          " Relative line numbers, together they are hybrid

set backspace=indent,eol,start  " Make backspace less limited

set linebreak               " Wordwrapping not in the middle of words

set showcmd                 " Show command in bottom bar
set cursorline              " Highlight current line

set clipboard=unnamed       " macOS clipboard
set ttymouse=sgr            " Fast modern mouse draggin
set mouse=a                 " Enable mouse support

filetype indent on          " Load filetype-specific indent files, also enables filetype detection

set wildmenu                " Visual autocompletion for command line

set lazyredraw              " Redraw only when we need, not in the middle of macros (faster)

set showmatch               " Highlight matching [{()}]
packadd! matchit

set scrolloff=3             " Start scrolling before reaching the first or last lines

set splitbelow              " Default to new splits below and to the right
set splitright

set laststatus=2            " Always show status line

" Statusline
" Function to pad the column line for less jittery movement
function! Statusline_col()
    let l:curCol=virtcol('.')

    if len(l:curCol) < 2
        let l:curCol=' '.l:curCol
    endif
    
    return l:curCol
endfunction

set statusline=\            " Space
set statusline+=\           " Space
set statusline+=%f          " Short filename
set statusline+=\           " Space
set statusline+=%m          " Modified flag
set statusline+=%r          " Readonly flag
set statusline+=%=          " Split point to right side
set statusline+=%y          " Filetype
set statusline+=\           " Space
set statusline+=Ln\ %l/%L   " Line number / Total lines
set statusline+=\ :\        " Separator
set statusline+=Col\        " Column number
set statusline+=%{Statusline_col()}
set statusline+=\ ☰\        " Separator
set statusline+=%p%%        " Percent through file
set statusline+=\           " Space
set statusline+=\           " Space

" special characters for formatting. set list to enable (<leader>l)
set listchars=nbsp:⦸
set listchars+=tab:▷┅
set listchars+=eol:¬
set listchars+=extends:»
set listchars+=precedes:«
set listchars+=trail:•

let &t_SI = "\<Esc>[6 q"    " use a solid | cursor in insert mode
let &t_EI = "\<Esc>[2 q"    " use a solid rectangle cursor otherwise

if exists('&belloff')
    set belloff=all         " never ring the bell for any reason
endif

" }}}

" Search {{{

set incsearch               " Search as characters are being typed
set hlsearch                " Keep highlighting matches of last search
set ignorecase              " Case insensitive search
set smartcase               " If uppercase letters are included in the search switch to case sensitive

" }}}

" Folding {{{

set foldenable              " Enable folding
set foldlevelstart=10       " Open most folds by default
set foldmethod=indent       " Fold based on indent level

" }}}

" Mappings {{{

let mapleader=","           " Leader mapping

set ttimeoutlen=1           " This fixes the <esc> delay issue

" Alternative mappings for <esc>
inoremap jj <esc>
inoremap jk <esc>

" Easier navigation between windows
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" Accept completion with Enter
imap <expr> <buffer> <silent> <CR> pumvisible() ? "\<C-y>" : "\<CR>"
smap <expr> <buffer> <silent> <CR> pumvisible() ? "\<C-y>" : "\<CR>"

" Quick folding with <space>
nnoremap <space> za

" move vertically by visual line (when lines are wrapping)
" nnoremap j gj
" nnoremap k gk

" Store relative line number jumps in the jumplist if they exceed a threshold.
nnoremap <expr> k (v:count > 5 ? "m'" . v:count : '') . 'k'
nnoremap <expr> j (v:count > 5 ? "m'" . v:count : '') . 'j'

" fzf mappings
nnoremap <silent> <leader>t :Files<CR>
nnoremap <silent> <leader>c :Colors<CR>
nnoremap <silent> <leader><enter> :Buffers<CR>

" Hide search highlights
nnoremap <leader><space> :nohlsearch<CR>

" Edit .vimrc
nnoremap <leader>ev :vsp $MYVIMRC<CR>

" Edit .zshrc
nnoremap <leader>ez :vsp ~/.zshrc<CR>

" Source .vimrc
nnoremap <leader>sv :source $MYVIMRC<CR>

" Toggle relative line numbers
nnoremap <leader>r :set relativenumber!<CR>

" Enable special characters
nnoremap <leader>l :set list!<CR>

" Enable spellchecking
nnoremap <leader>s :set spell!<CR>

" <Leader><Leader> -- Open last buffer.
nnoremap <Leader><Leader> <C-^>

" Make current buffer/window full screen
nnoremap <Leader>o :only<CR>
" }}}

" Backups {{{

if exists('$SUDO_USER')
    set nobackup                        " Don't create backups as root
    set nowritebackup
else
    set backupdir=~/.vim/tmp/backup     " Keeps backup files out of the way
    set backupdir+=.
endif

if exists('$SUDO_USER')
    set noswapfile                      " Don't create root-owned files
else
    set directory=~/.vim/tmp/swap//     " Keeps swap files out of the way
    set directory+=.
endif

if exists('$SUDO_USER')
    set viminfo=                        " Don't create root-owned files
else
    set viminfo+=n~/.vim/tmp/viminfo    " Override ~/.viminfo default
endif

if has('mksession')
    set viewdir=~/.vim/tmp/view         " Override ~/.vim/view default
endif

" }}}

set modelines=1
" vim: foldmethod=marker foldlevel=0
