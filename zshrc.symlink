#!/usr/bin/env zsh

# rbenv setup {{{
# --no-hash makes it quicker and it's enough to run after installations
eval "$(rbenv init - --no-rehash)"
# }}}

# Antigen {{{

# Installs antigen if it's not found
if [[ ! -f ~/.antigen/antigen.zsh ]]; then
    mkdir -v ~/.antigen
    curl -L git.io/antigen > ~/.antigen/antigen.zsh
fi
source ~/.antigen/antigen.zsh

antigen use oh-my-zsh
antigen bundle z
antigen bundle zuxfoucault/colored-man-pages_mod
antigen bundle gem
antigen bundle web-search
antigen bundle zdharma/fast-syntax-highlighting
antigen bundle zdharma/history-search-multi-word

antigen bundle mafredri/zsh-async
antigen bundle sindresorhus/pure

antigen apply

# }}}

# Exports {{{

export LSCOLORS=gxfxcxdxbxegedabagacad
# export EDITOR=code
export EDITOR=vim
# R: raw output, to allow ANSI colors
# M: verbose prompt, line numbers/ percentage
export LESS="-iRM"
# Options for grep
export GREP_OPTIONS="-i"
# Stops Homebrew from using my GitHub accound and returning errors
export HOMEBREW_NO_GITHUB_API=1

# }}}

# Aliases {{{

# Shortcuts
alias c=clear
alias h=history
alias j=z
alias u=users
alias t=tmux
alias vimr="vim -M"

# File management
alias la="ls -a"
alias ll="ls -lh"
alias lla="ls -lha"
alias mkdir="mkdir -pv"
alias du="du -h"

# Git
alias gs="git status -sb"

# MacOs
alias showfiles="defaults write com.apple.finder AppleShowAllFiles YES && killall Finder"
alias hidefiles="defaults write com.apple.finder AppleShowAllFiles NO && killall Finder"
alias emptytrash="trash -e"
alias battery="pmset -g batt"

# Editing
alias zshrc="$EDITOR ~/.zshrc"
alias reload="source ~/.zshrc"
alias todo="$EDITOR ~/todo.md"

# IP addresses
alias ip="dig +short myip.opendns.com @resolver1.opendns.com"
alias localip="ipconfig getifaddr en0"
alias ips="ifconfig -a | grep -o 'inet6\? \(addr:\)\?\s\?\(\(\([0-9]\+\.\)\{3\}[0-9]\+\)\|[a-fA-F0-9:]\+\)' | awk '{ sub(/inet6? (addr:)? ?/, \"\"); print }'"

# Show active network interfaces
alias ifactive="ifconfig | pcregrep -M -o '^[^\t:]+:([^\n]|\n\t)*status: active'"

# Global aliases
alias -g G="|grep"
alias -g L="|less"
alias -g Y="|yank"
alias -g C="|lolcat"
alias -g H="|head"
alias -g T="|tail"

# }}}

# Functions {{{

# Usage: compresspdf [input file] [output file] [screen*|ebook*|printer|prepress]
compresspdf() {
    \gs -sDEVICE=pdfwrite -dNOPAUSE -dQUIET -dBATCH -dPDFSETTINGS=/${3:-"ebook"} -dCompatibilityLevel=1.4 -sOutputFile="$2" "$1"
}

tmux() {
    if [[ -n "$@" ]]; then
        command tmux "$@"
        return
    fi

    if [[ "$TERM" != "screen" ]] && 
        [[ "$SSH_CONNECTION" == "" ]] &&
            [[ -z "$TMUX" ]]; then
        # Attempt to discover a detached session and attach 
        # it, else create a new session


        SESSION_NAME=base
        command tmux new -A -s $SESSION_NAME
    fi
}

o() {
    open ${1:-"."}
}

# }}}

# fzf {{{

# Load fzf tab completion and key bindings
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# Use fzf to cd
fd() {
    local dir
    dir=$(find . -maxdepth 1 -path './.*' -prune -o -type d -print | sed '1d;s#^./##' |
    fzf --height 20 --reverse --query "$1" --select-1 --exit-0) && cd "$dir"
}

# Combine z and fzf
unalias z 2> /dev/null
z() {
    [ $# -gt 0 ] && _z "$*" && return
    cd "$(_z -l 2>&1 | fzf --height 40% --nth 2.. --reverse --inline-info +s --tac --query "${*##-* }" | sed 's/^[0-9,.]* *//')"
}

# }}}

# Start tmux
tmux

# vim:foldmethod=marker:foldlevel=0
