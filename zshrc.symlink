#!/usr/bin/env zsh

# Setup rbenv 

# --no-hash makes it quicker and it's enough to run after installations
eval "$(rbenv init - --no-rehash)"

# Antigen

# Installs antigen if it's not found
if [[ ! -f ~/.antigen/antigen.zsh ]]; then
    mkdir -v ~/.antigen
    curl -L git.io/antigen > ~/.antigen/antigen.zsh
fi
source ~/.antigen/antigen.zsh


antigen bundle mafredri/zsh-async               # async support for pure theme
antigen bundle sindresorhus/pure                # My preferred minimal theme
antigen bundle zsh-users/zsh-autosuggestions    # Autosuggestions
antigen bundle zdharma/fast-syntax-highlighting # Syntax highlighting

antigen apply

# History

export HISTFILE=$HOME/.zsh_history      # History file
export HISTSIZE=10000                   # Maximum number of events stored internally
export SAVEHIST=10000                   # Maximum number of events stored in the file


# Bindkey

bindkey -e                              # Emacs style bindings
bindkey ' ' magic-space                 # Magic space for history expansion
bindkey '^r' history-incremental-search-backward # History search
bindkey "^[[3~" delete-char             # Delete key
bindkey '^u' backward-kill-line         # Delete from cursor to beginning
bindkey '^[[Z' reverse-menu-complete    # Shift-tab for moving backwards in menus

# Up/Down arrows history search
autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey '^[[A' history-beginning-search-backward-end
bindkey '^[[B' history-beginning-search-forward-end

# Options

setopt appendhistory
setopt autocd
setopt autopushd
setopt extendedhistory
setopt histexpiredupsfirst
setopt histfindnodups
setopt histignoredups
setopt histignorespace
setopt histverify
setopt incappendhistory
setopt interactivecomments
setopt pushdignoredups
setopt sharehistory

# Completion colors: empty string ("") would use the shell's defaults ($LSCOLORS),
# but zsh doesn't support BSD style $LSCOLORS so here is the equivalent in Linux style $LS_COLORS
zstyle ":completion:*" \
    list-colors "di=34:ln=35:so=32:pi=33:ex=31:bd=34;46:cd=34;43:su=30;41:sg=30;46:tw=30;42:ow=30;43"
# Case insensitive autocomplete
zstyle ":completion:*" \
    matcher-list "m:{a-zA-Z}={A-Za-z}" "r:|[._-]=* r:|=*" "l:|=* r:|=*"
# Show a menu for completions
zstyle ":completion:*" menu select
# Kill processes autocomplete
zstyle ":completion:*:*:kill:*:processes" \
    list-colors "=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01"
zstyle ":completion:*:*:*:*:processes" \
    command "ps -u `whoami` -o pid,user,comm -w -w"
# Users autocomplete, hide irrelelvant users
zstyle ":completion:*:*:*:users" ignored-patterns \
    adm amanda apache at avahi avahi-autoipd beaglidx bin cacti canna \
    clamav daemon dbus distcache dnsmasq dovecot fax ftp games gdm \
    gkrellmd gopher hacluster haldaemon halt hsqldb ident junkbust kdm \
    ldap lp mail mailman mailnull man messagebus  mldonkey mysql nagios \
    named netdump news nfsnobody nobody nscd ntp nut nx obsrun openvpn \
    operator pcap polkitd postfix postgres privoxy pulse pvm quagga radvd \
    rpc rpcuser rpm rtkit scard shutdown squid sshd statd svn sync tftp \
    usbmux uucp vcsa wwwrun xfs "_*"
# Disable named-directories autocompletion
zstyle ":completion:*:cd:*" tag-order local-directories directory-stack \
    path-directories

# Frecency based directory switching
[ -n /usr/local/etc/profile.d/z.sh ] && source /usr/local/etc/profile.d/z.sh

# Load fzf tab completion and key bindings
[ $(command -v fzf) ] && [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# Environment variables
source $HOME/.shell/env
# Aliases
source $HOME/.shell/aliases
# Functions
source $HOME/.shell/functions

start_tmux
